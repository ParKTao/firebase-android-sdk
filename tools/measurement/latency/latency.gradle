// Copyright 2019 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import com.google.firebase.gradle.plugins.measurement.UploadMeasurementsTask

// Requires nodejs and npm (node package manager) in the environment.
task buildLatencyProject(type: Exec) {
    description "Build typescript project for collecting prow jobs info."

    executable "npm"
    args "install"
}

// Requires nodejs and npm (node package manager) in the environment.
task generateLatencyMeasurements(type: Exec) {
    description "Query prow jobs latency information from k8s cluster."
    group "Measurements"

    dependsOn buildLatencyProject

    executable "npm"
    args "start", "--", "--reportFile=$buildDir/latency-report.json"
}

task uploadLatencyMeasurements(type: UploadMeasurementsTask) {
    description "Uploads latency measurements to a database via the uploader tool."
    group "Measurements"

    dependsOn generateLatencyMeasurements
    reportFile = file("$buildDir/latency-report.json")
    uploader = "https://storage.googleapis.com/firebase-engprod-metrics/upload_tool.jar"
}
